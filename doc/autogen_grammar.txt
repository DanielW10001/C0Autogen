<空>::=""
<加法运算符>::="+"|"-"
<乘法运算符>::="*"|"/"
<关系运算符>::="<"|"<="|">"|">="|"!="|"=="
<字母>::="_"|"a"|"b"|"c"|"d"|"e"|"f"|"g"|"h"|"i"|"j"|"k"|"l"|"m"|"n"|"o"|"p"|"q"|"r"|"s"|"t"|"u"|"v"|"w"|"x"|"y"|"z"|"A"|"B"|"C"|"D"|"E"|"F"|"G"|"H"|"I"|"J"|"K"|"L"|"M"|"N"|"O"|"P"|"Q"|"R"|"S"|"T"|"U"|"V"|"W"|"X"|"Y"|"Z"
<数字>::="0"|<非零数字>
<非零数字>::="1"|"2"|"3"|"4"|"5"|"6"|"7"|"8"|"9"
<字符>::="\'"<加法运算符>"\'"|"\'"<乘法运算符>"\'"|"\'"<字母>"\'"|"\'"<数字>"\'"
<字符串>::="\"字符串\""
<程序>::=[<常量说明>][<变量说明>]{<有返回值函数定义>|<无返回值函数定义>}<主函数>
<常量说明>::="const "<常量定义>";\n"{"const "<常量定义>";\n"}
<常量定义>::="int "<标识符>"="<整数>{", "<标识符>"="<整数>}|"char "<标识符>"="<字符>{", "<标识符>"="<字符>}
<无符号整数>::=<非零数字>{<数字>}|"0"
<整数>::=["+"|"-"]<无符号整数>
<标识符>::="标识符"
<声明头部>::="int "<标识符>|"char "<标识符>
<变量说明>::=<变量定义>";\n"{<变量定义>";\n"}
<变量定义>::=<类型标识符>" "(<标识符>|<标识符>"["<无符号整数>"]"){", "(<标识符>|<标识符>"["<无符号整数>"]")}//<无符号整数>表示数组元素的个数,其值需大于0
<类型标识符>::="int"|"char"
<有返回值函数定义>::=<声明头部>"("<参数表>")\n""{\n"<复合语句>"}\n"
<无返回值函数定义>::="void "<标识符>"("<参数表>")\n""{\n"<复合语句>"}\n"
<复合语句>::=[<常量说明>][<变量说明>]<语句列>
<参数表>::=<类型标识符>" "<标识符>{", "<类型标识符>" "<标识符>}|<空>
<主函数>::="void"" ""main""("")\n""{\n"<复合语句>"}\n"
<表达式>::=["+"|"-"]<项>{<加法运算符><项>}//["+"|"-"]只作用于第一个<项>
<项>::=<因子>{<乘法运算符><因子>}
<因子>::=<标识符>|<标识符>"["<表达式>"]"|"("<表达式>")"|<整数>|<字符>|<有返回值函数调用语句>
<语句>::=<条件语句>|<循环语句>|"{\n"<语句列>"}\n"|<有返回值函数调用语句>";\n"|<无返回值函数调用语句>";\n"|<赋值语句>";\n"|<读语句>";\n"|<写语句>";\n"|<空>";\n"|<返回语句>";\n"
<赋值语句>::=<标识符>"="<表达式>|<标识符>"["<表达式>"]""="<表达式>
<条件语句>::="if""("<条件>")\n"<语句>["else\n"<语句>]
<条件>::=<表达式><关系运算符><表达式>|<表达式>//值为0为假,否则为真
<循环语句>::="while""("<条件>")\n"<语句>|"do\n"<语句>"while""("<条件>")\n"|"for""("<标识符>"="<表达式>";"<条件>";"<标识符>"="<标识符>("+"|"-")<步长>")\n"<语句>
<步长>::=<无符号整数>
<有返回值函数调用语句>::=<标识符>"("<值参数表>")"
<无返回值函数调用语句>::=<标识符>"("<值参数表>")"
<值参数表>::=<表达式>{", "<表达式>}|<空>
<语句列>::={<语句>}
<读语句>::="scanf""("<标识符>{", "<标识符>}")"
<写语句>::="printf""("<字符串>", "<表达式>")"|"printf""("<字符串>")"|"printf""("<表达式>")"
<返回语句>::="return"["("<表达式>")"]

